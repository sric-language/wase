
import sric::*
import waseGui::*
import waseGraphics::*
import serial::*


struct Viewer {
    var lastUpdateTime1: Int64 = 0
    var lastUpdateTime2: Int64 = 0
    var frame : own*? Frame

    fun loadView(viewFile: raw*? const Int8, styleFile: raw*? const Int8) {

        if (viewFile != null) {
            var fileTime = FileSystem::modifiedTime(viewFile)
            if (fileTime > lastUpdateTime1) {
                printf("Reload %s\n", viewFile);
                frame.loadView(viewFile)
                lastUpdateTime1 = fileTime
            }
        }

        if (styleFile != null) {
            var fileTime = FileSystem::modifiedTime(styleFile)
            if (fileTime > lastUpdateTime2) {
                printf("Reload %s\n", styleFile);
                frame.loadStyle(styleFile)
                lastUpdateTime2 = fileTime
            }
        }

        waseGraphics::setTimeout(1000, fun(){
            this.loadView(viewFile, styleFile)
        });
    }

    fun run(viewFile: raw*const Int8, styleFile: raw*? const Int8) {
        frame = new Frame { .name = "Wase Viewer" }

        loadView(viewFile, styleFile)

        frame.show()
    }
}

fun printHelp() {
    printf("
            Usage:
              waseViewer [options] <viewFile>
            Arguments:
              viewFile           HiML view file
            Options:
              -s <Str>           HiML style file
              -help, -?          print usage help
            ");
}

unsafe fun main(argc: Int, argv: raw* raw* const Int8): Int {

    var viewFile: raw*? const Int8
    var styleFile: raw*? const Int8

    for (var i = 1; i<argc; ++i) {
        if (strcmp(argv[i], "-s") == 0) {
            ++i;
            if (i == argc) break;
            styleFile = argv[i];
        }
        else if (strcmp(argv[i], "-?") == 0 || strcmp(argv[i], "-help") == 0) {
            printHelp();
            return 0;
        }
        else if (argv[i][0] == '-') {
            printf("Unknow options: %s\n", argv[i]);
            return -1;
        }
        else {
            if (viewFile == null) {
                viewFile = argv[i];
            }
            else {
                printf("Unknow args: %s\n", argv[i]);
                return -1;
            }
        }
    }

    if (viewFile == null) {
        printHelp()
        return -1;
    }

    var viewer : Viewer
    viewer.run(viewFile, styleFile)

    return 0
}
